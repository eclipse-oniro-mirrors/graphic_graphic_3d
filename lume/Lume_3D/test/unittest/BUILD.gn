# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Declarations
import("//build/test.gni")
import("//build/ohos.gni")
import("//foundation/graphic/graphic_3d/lume/lume_config.gni")

# Test case compilation output path
module_output_path = "graphic_3d/lume/Lume3D"

# Options
RENDER_BUILD_GLES = true
RENDER_BUILD_VULKAN = true

#
# Lume3D API test config
#

#
# Unit tests Lume3D api config 
#
config("lume_3d_test_config") {

  # Only targets in the current buildfile ("private")
  visibility = [":*"]
  
  # Unit test defines
  defines = [
    "OHOS_PLATFORM_ASSETS_PATH=\"/data/test/resource/graphic_3d/Lume3D/test_data/\"",
    "CORE_PERF_ENABLED=0",
    "RENDER_PERF_ENABLED=0"
  ]

  if (RENDER_BUILD_GLES) {
    defines += [
      "RENDER_HAS_GLES_BACKEND=1"
    ]
  }

  if (RENDER_BUILD_VULKAN) {
    defines += [
      "RENDER_HAS_VULKAN_BACKEND=1"
    ]
  }
}

config("lume_3d_test_dynamic_lib_config") {
  # Only targets in the current buildfile ("private")
  visibility = [":*"]
  
  defines = [
    "CORE_DYNAMIC=1"
  ]
}

# ohos_unittest
ohos_unittest("lume_3d_api_test") {

  module_out_path = module_output_path

  # Configs
  configs = [
    "${LUME_BASE_PATH}:lume_base_api_config",
    "${LUME_CORE_PATH}:lume_engine_api",
    "${LUME_CORE_PATH}:lume_component_help_config",
    "${LUME_RENDER_PATH}:lume_render_api",
    "${LUME_PNG_PATH}:lume_png_api",
    "${LUME_JPG_PATH}:lume_jpg_api",
    "${LUME_CORE3D_PATH}:lume_3d_api",

    ":test_plugin_shared_config",
    "${LUME_BASE_PATH}:lume_unit_test_config",
    ":lume_3d_test_config",
    ":lume_3d_test_dynamic_lib_config"
  ]
  
  # Includes
  include_dirs = [
    "common",
    "api_unit_test/src",
  ]

  # Src
  sources = [
    "common/load_lib.cpp",
    "common/test_runner_ohos_system.cpp",

    # ECS - Components
    "api_unit_test/src/ecs/components/base_manager_test.cpp",
    "api_unit_test/src/ecs/components/material_component_manager_test.cpp",
    "api_unit_test/src/ecs/components/post_process_configuration_component_manager_test.cpp",
    "api_unit_test/src/ecs/components/render_handle_component_manager_test.cpp",

    # ECS - Systems
    "api_unit_test/src/ecs/systems/animation_playback_test.cpp",
    "api_unit_test/src/ecs/systems/animation_system_test.cpp",
    "api_unit_test/src/ecs/systems/morphing_system_test.cpp",
    "api_unit_test/src/ecs/systems/node_system_test.cpp",
    "api_unit_test/src/ecs/systems/render_system_test.cpp",
    "api_unit_test/src/ecs/systems/skinning_system_test.cpp",

    # Gfx
    # Some GFX tests require CORE_PERF_ENABLED=1
    "api_unit_test/src/gfx/ecs_events/ecs_default_events.cpp",
    "api_unit_test/src/gfx/gfx_animation_test.cpp",
    "api_unit_test/src/gfx/gfx_area_light_test.cpp",
    "api_unit_test/src/gfx/gfx_cloud_test.cpp",
    "api_unit_test/src/gfx/gfx_cloud_test.h",
    "api_unit_test/src/gfx/gfx_common.cpp",
    "api_unit_test/src/gfx/gfx_common.h",
    "api_unit_test/src/gfx/gfx_culling_test.cpp",
    "api_unit_test/src/gfx/gfx_culling_test.h",
    "api_unit_test/src/gfx/gfx_hello_world_boilerplate.cpp",
    "api_unit_test/src/gfx/gfx_hello_world_boilerplate.h",
    "api_unit_test/src/gfx/gfx_multi_ecs.cpp",
    "api_unit_test/src/gfx/gfx_shader_custom_properties.cpp",
    "api_unit_test/src/gfx/gfx_test.cpp",
    "api_unit_test/src/gfx/gfx_water_ripple_test.cpp",

    # GPU
    "api_unit_test/src/gpu/defaultmaterial/gpu_test_default_material_test.cpp",
    "api_unit_test/src/gpu/ecs/gpu_test_ecs_test.cpp",
    "api_unit_test/src/gpu/ecsresource/gpu_test_ecs_resource_test.cpp",
    "api_unit_test/src/gpu/node_system/gpu_test_node_system_test.cpp",

    # Plugin
    "api_unit_test/src/plugin/plugin_test.cpp",
    
    # Render
    "api_unit_test/src/render/graphics_context_test.cpp",
    "api_unit_test/src/render/render_data_store_default_camera_test.cpp",
    "api_unit_test/src/render/render_data_store_default_light_test.cpp",
    "api_unit_test/src/render/render_data_store_default_material_test.cpp",
    "api_unit_test/src/render/render_data_store_default_scene_test.cpp",
    "api_unit_test/src/render/render_data_store_morph_test.cpp",

    # Util
    "api_unit_test/src/util/mesh_builder_test.cpp",
    "api_unit_test/src/util/picking_test.cpp",
    "api_unit_test/src/util/render_util_test.cpp",
    "api_unit_test/src/util/scene_util_test.cpp",      
  ]

  # External deps
  external_deps = [
    "bounds_checking_function:libsec_shared",
    "googletest:gmock",
    "googletest:gtest",
    "c_utils:utils",
    "graphic_surface:surface",
    "graphic_2d:libnative_image", # Handle HOS window
  ]

  if (RENDER_BUILD_GLES) {
    external_deps += [
      "graphic_2d:EGL",
      "graphic_2d:GLESv3"
    ]
  }

  if (RENDER_BUILD_VULKAN) {
    external_deps += [
      "vulkan-headers:vulkan_headers",
      "vulkan-loader:vulkan_loader",
    ]
  }

  # Deps
  deps = [
    "${LUME_CORE_PATH}/ecshelper:libAGPEcshelper", # Static lib!
    ":libCore3DTestSharedPlugin",
    ":PushTestResource"
  ]

  # Test data
  resource_config_file = "//foundation/graphic/graphic_3d/lume/Lume_3D/test/unittest/assets/ohos_test.xml"

  # graphic/graphic_3d
  part_name = "graphic_3d"
  subsystem_name = "graphic"
}

#
# Lume3D SRC test config
#

config("lume_3d_static_lib_config") {

  # Only targets in the current buildfile ("private")
  visibility = [":*"]
  
  # Using static lib
  defines = [
    "CORE3D_STATIC_LIB",
  ]
}

# ohos_unittest
ohos_unittest("lume_3d_src_test") {

  module_out_path = module_output_path

  # Configs
  configs = [
    "${LUME_BASE_PATH}:lume_base_api_config",
    "${LUME_CORE_PATH}:lume_engine_api",
    "${LUME_CORE_PATH}:lume_component_help_config",
    "${LUME_RENDER_PATH}:lume_render_api",
    "${LUME_PNG_PATH}:lume_png_api",
    "${LUME_JPG_PATH}:lume_jpg_api",
    "${LUME_CORE3D_PATH}:lume_3d_api",
    "${LUME_CORE3D_PATH}:lume_3d_config",

    ":test_plugin_shared_config",
    "${LUME_BASE_PATH}:lume_unit_test_config",
    ":lume_3d_test_config",
    ":lume_3d_static_lib_config"
  ]

  # Includes
  include_dirs = [
    "common",
    "src_unit_test/src"
  ]

  # Src
  sources = [
    "common/test_runner_ohos_system.cpp",

    # ECS - Components
    "src_unit_test/src/ecs/components/base_manager_test.cpp",

    # ECS - Systems
    "src_unit_test/src/ecs/systems/local_matrix_system_test.cpp",
    "src_unit_test/src/ecs/systems/render_preprocessor_system_test.cpp",

    # GLTF
    "src_unit_test/src/gltf/gltf_loader_test.cpp",
    "src_unit_test/src/gltf/gltf_exporter_test.cpp",
    "src_unit_test/src/gltf/gltf_importer_test.cpp",
    "src_unit_test/src/gltf/gltf_util_test.cpp",
    
    # GPU
    "src_unit_test/src/gpu/ecsresource/gpu_test_ecs_resource_test.cpp",
    "src_unit_test/src/gpu/gltf/gpu_test_gltf_importer_test.cpp",

    # Render
    "src_unit_test/src/render/render_data_store_morph_test.cpp",
    "src_unit_test/src/render/render_data_store_weather_test.cpp",
    "src_unit_test/src/render/render_node_camera_single_post_process_test.cpp",
    "src_unit_test/src/render/render_node_default_camera_post_process_interface_controller_test.cpp",
    "src_unit_test/src/render/render_node_scene_util_test.cpp",

    # Util
    "src_unit_test/src/util/property_util_test.cpp",
  ]

  # External deps
  external_deps = [
    "bounds_checking_function:libsec_shared",
    "googletest:gmock",
    "googletest:gtest",
    "c_utils:utils",
    "graphic_surface:surface",
    "graphic_2d:libnative_image", # Handle HOS window
  ]

  if (RENDER_BUILD_GLES) {
    external_deps += [
      "graphic_2d:EGL",
      "graphic_2d:GLESv3"
    ]
  }

  if (RENDER_BUILD_VULKAN) {
    external_deps += [
      "vulkan-headers:vulkan_headers",
      "vulkan-loader:vulkan_loader",
    ]
  }

  # Deps
  deps = [
    # Using static lib to test Lume3D src
    "${LUME_CORE_PATH}/DLL:libAGPDLL",
    "${LUME_RENDER_PATH}:libPluginAGPRender",
    "${LUME_PNG_PATH}:libPluginAGPPng",
    "${LUME_JPG_PATH}:libPluginAGPJpg",
    ":libStaticAGP3D",

    ":libCore3DTestSharedPlugin",
  ]

  # Test data
  resource_config_file = "//foundation/graphic/graphic_3d/lume/Lume_3D/test/unittest/assets/ohos_test.xml"


  # graphic/graphic_3d
  part_name = "graphic_3d"
  subsystem_name = "graphic"
}

# group ("unittest")
group("unittest") {
    testonly = true
    deps = [ 
        ":lume_3d_api_test",
        ":lume_3d_src_test"
    ]
}

#
# Lume3D Static lib
# 
ohos_source_set("lume_3d_src_static") {
  visibility = [ ":*" ]
  
  sources = [
    #"../../src/plugin/static_plugin.cpp", # already in lume_3d_config
    "../../src/plugin/static_registry.cpp",
    #"../../src/util/json.cpp", # JSON_IMPL is defined in the test src code
  ]

  configs = [
    "${LUME_BASE_PATH}:lume_base_api_config",
    "${LUME_CORE_PATH}:lume_engine_api",
    "${LUME_CORE_PATH}:lume_component_help_config",
    "${LUME_RENDER_PATH}:lume_render_api",
    "${LUME_PNG_PATH}:lume_png_api",
    "${LUME_JPG_PATH}:lume_jpg_api",
    "${LUME_CORE_PATH}:lume_default",
    "${LUME_CORE3D_PATH}:lume_3d_config",
  ]
}

ohos_static_library("libStaticAGP3D") {
  visibility = [ ":*" ]

  deps = [
    "${LUME_CORE3D_PATH}:lume_3d_src",
    "${LUME_CORE3D_PATH}:lume_3d_rofs_obj",
    ":lume_3d_src_static"
  ]
  
  part_name = "graphic_3d"
  subsystem_name = "graphic"
}

#
# Test plugin lib (SHARED) config
#
config("test_plugin_shared_config") {

  # Only targets in the current buildfile ("private")
  visibility = [":*"]

  # Includes
  include_dirs = [
    "test_plugin/src"
  ]

  defines = [
    "CORE_PLUGIN=1"
  ]
}

#
# Test plugin lib (SHARED) sources
#
ohos_source_set("core3d_test_plugin_shared_src") {
  public_configs = [
    ":test_plugin_shared_config",
    "${LUME_BASE_PATH}:lume_base_api_config",
    "${LUME_CORE_PATH}:lume_engine_api",
    "${LUME_RENDER_PATH}:lume_render_api",
    "${LUME_PNG_PATH}:lume_png_api",
    "${LUME_JPG_PATH}:lume_jpg_api",
    "${LUME_CORE3D_PATH}:lume_3d_api"
  ]

  sources = [
    "test_plugin/src/3d_test_plugin.cpp"
  ]
}

#
# Test plugin lib (SHARED) 
#
ohos_shared_library("libCore3DTestSharedPlugin") {
  deps = [
    ":core3d_test_plugin_shared_src"
  ]

  part_name = "graphic_3d"
  subsystem_name = "graphic"
}

action("PushTestResource") {
  outputs = [ "$target_out_dir/out.txt" ]
  script = "${LUME_CORE3D_PATH}/test/unittest/assets/copy.sh"
  deps = [ ":libCore3DTestSharedPlugin" ]
  args = [
    "$target_out_dir",
  ]
}

